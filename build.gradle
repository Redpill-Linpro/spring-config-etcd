apply plugin: 'java' 
apply plugin: 'maven-publish'

group = "com.redpill_linpro.springframework"
version = "0.1.0"
description = 'A PropertyPlaceholderConfigurer that loads properties from a etcd key-value store'

compileJava {
    sourceCompatibility=1.6
    targetCompatibility=1.6
}

compileTestJava {
    sourceCompatibility=1.7
    targetCompatibility=1.7
}

// Product versions
ext {
    springVersion='3.2.+'
    jetcdVersion='0.2.+'
    junitVersion='4.+'
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.springframework:spring-beans:${springVersion}"
    compile "net.floatingsun:jetcd:${jetcdVersion}"
    testCompile "org.springframework:spring-test:${springVersion}"
    testCompile "junit:junit:${junitVersion}"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
    repositories {
        maven {
            url "$buildDir/repo" // change to point to your repo, e.g. http://my.org/repo
        }
    }
}

// Example source set
sourceSets {
    examples {
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + sourceSets.main.output + configurations.testRuntime
    }
}

task examples(type: Test) {
    testClassesDir = sourceSets.examples.output.classesDir
    classpath = sourceSets.examples.runtimeClasspath
}


task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}
